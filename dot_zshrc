# history
HISTSIZE=10000
SAVEHIST=100000
HISTFILE=~/.zsh_history
setopt INC_APPEND_HISTORY
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_REDUCE_BLANKS
setopt HIST_IGNORE_SPACE
setopt HIST_NO_STORE
setopt CORRECT

bindkey '^[[A' up-line-or-search
bindkey '^[[B' down-line-or-search

# editing
bindkey -e
autoload -U select-word-style
select-word-style bash

# completion
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' menu select=2
zstyle :compinstall filename "$HOME/.zshrc"

# personal completion
fpath=(~/.zsh/completion $fpath)

# personal path
path+=("$HOME/bin")
export PATH

autoload -Uz compinit && compinit

setopt auto_menu

# editor
if command -v vim &>/dev/null; then
    export EDITOR=vim
else
    export EDITOR=vi
fi

# ls
case $(uname) in
    Darwin)
        export CLICOLOR_FORCE=1
        alias ls="\ls -bCFGh"
        alias ll="\ls -bCFGh -l"
        alias la="\ls -bCFGh -la"
        ;;

    Linux)
        alias ls="\ls --color=always -bCFh"
        alias ll="\ls --color=always -bCFh -l"
        alias la="\ls --color=always -bCFh -la"
        ;;
esac

# less
export LESS="-FRXi"

# lesspipe
if command -v lesspipe &>/dev/null; then
    eval $(lesspipe) &>/dev/null
elif command -v lesspipe.sh &>/dev/null; then
    export LESSOPEN="| $(command -v lesspipe.sh) %s"
    export LESS_ADVANCED_PREPROCESSOR=1
elif command -v src-hilite-lesspipe.sh &>/dev/null; then
    export LESSOPEN="| src-hilite-lesspipe.sh %s"
elif [[ -f /usr/share/source-highlight/src-hilite-lesspipe.sh ]]; then
    export LESSOPEN="| /usr/share/source-highlight/src-hilite-lesspipe.sh %s"
fi

# man
man() {
    env \
    LESS_TERMCAP_mb=$(printf "\e[1;31m") \
    LESS_TERMCAP_md=$(printf "\e[1;31m") \
    LESS_TERMCAP_me=$(printf "\e[0m") \
    LESS_TERMCAP_se=$(printf "\e[0m") \
    LESS_TERMCAP_so=$(printf "\e[1;44;33m") \
    LESS_TERMCAP_ue=$(printf "\e[0m") \
    LESS_TERMCAP_us=$(printf "\e[1;32m") \
    man "$@"
}

# screen and tmux
alias scr="screen -d -RR"
alias t="tmux new -A -D -s main"

# prompt
if [[ $TERM == "screen" ]]; then
    _GET_PATH='echo $PWD | sed "s/^\/Users\//~/;s/^~$USER/~/"'
    TAB_TITLE_PREFIX='"`'$_GET_PATH' | sed "s:..*/::"`$PROMPT_CHAR"'

    function preexec()
    {
        local -a cmd=(${(z)1})
        print -nR $'\033k'$cmd[1]:t$'\033'\\\
    }

    function precmd()
    {
        eval "tab_title=\"$TAB_TITLE_PREFIX|$SHELL:t\""
        print -nR $'\033k'$tab_title$'\033'\\\
    }
fi

# colors
autoload -U colors && colors
PROMPT="%{$fg[red]%}%m%{$reset_color%}:%{$fg[blue]%}%~%{$reset_color%} %# "

# vcs info
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst
RPROMPT=\$vcs_info_msg_0_
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' get-revision true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' formats '[%b]%u%c-'
zstyle ':vcs_info:*' actionformats '[%b|%a]%u%c-'

# direnv
if command -v direnv &>/dev/null; then
    eval "$(direnv hook zsh)"
    export DIRENV_LOG_FORMAT=
fi

# fzf
if command -v fzf &>/dev/null && [[ $- == *i* ]]; then
    eval "$(fzf --zsh)"
fi

# zoxide
command -v zoxide &>/dev/null && eval "$(zoxide init --cmd j zsh)"

# completion

# completion for homebrew software
if command -v brew &>/dev/null; then
    fpath=($(brew --prefix)/share/zsh-completions $fpath)
    fpath=($(brew --prefix)/share/zsh/site-functions $fpath)
fi

# sets completion for a command the same as another (https://unix.stackexchange.com/questions/496379/treat-command-like-another-for-completion-purposes)
compdefas () {
  if (($+_comps[$1])); then
    compdef $_comps[$1] ${^@[2,-1]}=$1
  fi
}

# autocompletes mvnd as if it were mvn
command -v mvnd &>/dev/null && compdefas mvn mvnd

# autosuggestions
ZSH_AUTOSUGGEST_STRATEGY=(history)
if command -v brew &>/dev/null && [[ -f $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
    . $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh
elif [[ -f /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
    . /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# zsh syntax highlighting
if command -v brew &>/dev/null && [[ -f $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    . $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
elif [[ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    . /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# sources local zshrc
[[ -f ~/.zsh/zshrc-local.zsh ]] && . ~/.zsh/zshrc-local.zsh
